/*
 * This is our main _"landing page"_ for our Hyperlambda help files, which
 * is a tutorial style _"book"_, teaching you Hyperlambda, from a beginner's
 * perspective.
 */

h2
  innerValue:Hyperlambda

img
  class:desktop-help-icon-image
  src:"https://phosphorusfive.files.wordpress.com/2018/03/lambda-hyper.png"

p
  innerValue:@"Welcome to <em>'The Guide'</em> to Hyperlambda. Hyperlambda is a programming language explicitly created for Phosphorus Five,
that allows you to rapidly create your own applications. This <em>'book'</em> will get you rapidly started with Hyperlambda,
in a couple of hours, by giving you practical hands on examples, that you can play around with on your own."

/*
 * All files at "root" of Hyperlambda help folder.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@DESKTOP/help-files/Hyperlambda/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp/#?name
        =:/
        =:.
      replace-x:x:/@split/0/-2?name
        src:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@replace-x?value
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              desktop.help.display-file:x:/@get-widget-property/*/*?value
